unit Procedimento.Svc;

interface

uses
  FireDAC.Comp.Client, ModelDB, Model.Procedimento, Model.Procedimento.Tipo,
  System.Generics.Collections, System.SysUtils;

type
  TProcedimentoSQLHandler = class
  private
    FDB: TModelDB;
  public
    constructor Create(ADB: TModelDB);

    function ListarTodos: TArray<TProcedimento>;
    function BuscarPorId(const Id: Int64): TProcedimento;
    function BuscarPorTipo(const Tipo: TTipoProcedimento): TArray<TProcedimento>;

    function Inserir(const Tipo: TTipoProcedimento; const Descricao: string; const Valor: Currency): Boolean;
    function Atualizar(const Id: Int64; const Tipo: TTipoProcedimento; const Descricao: string; const Valor: Currency): Boolean;
    function Excluir(const Id: Int64): Boolean;

    function Carregar(const Id: Int64; Procedimento: TProcedimento): Boolean;
  end;

implementation

{ TProcedimentoSQLHandler }

constructor TProcedimentoSQLHandler.Create(ADB: TModelDB);
begin
  FDB := ADB;
end;

function TProcedimentoSQLHandler.ListarTodos: TArray<TProcedimento>;
var
  Query: TFDQuery;
  Lista: TList<TProcedimento>;
  P: TProcedimento;
begin
  Lista := TList<TProcedimento>.Create;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT * FROM procedimentos ORDER BY tipo';
    Query.Open;
    while not Query.Eof do
    begin
      P := TProcedimento.Create;
      P.Id := Query.FieldByName('id').AsLargeInt;
      P.Tipo := TTipoProcedimento(Query.FieldByName('tipo').AsInteger);
      P.Descricao := Query.FieldByName('descricao').AsString;
      P.Valor := Query.FieldByName('valor').AsCurrency;
      Lista.Add(P);
      Query.Next;
    end;
    Result := Lista.ToArray;
  finally
    Query.Free;
    Lista.Free;
  end;
end;

function TProcedimentoSQLHandler.BuscarPorId(const Id: Int64): TProcedimento;
var
  Query: TFDQuery;
begin
  Result := nil;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT * FROM procedimentos WHERE id = :id';
    Query.ParamByName('id').AsLargeInt := Id;
    Query.Open;
    if not Query.IsEmpty then
    begin
      Result := TProcedimento.Create;
      Result.Id := Query.FieldByName('id').AsLargeInt;
      Result.Tipo := TTipoProcedimento(Query.FieldByName('tipo').AsInteger);
      Result.Descricao := Query.FieldByName('descricao').AsString;
      Result.Valor := Query.FieldByName('valor').AsCurrency;
    end;
  finally
    Query.Free;
  end;
end;

function TProcedimentoSQLHandler.BuscarPorTipo(const Tipo: TTipoProcedimento): TArray<TProcedimento>;
var
  Query: TFDQuery;
  Lista: TList<TProcedimento>;
  P: TProcedimento;
begin
  Lista := TList<TProcedimento>.Create;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT * FROM procedimentos WHERE tipo = :tipo ORDER BY descricao';
    Query.ParamByName('tipo').AsInteger := Ord(Tipo);
    Query.Open;
    while not Query.Eof do
    begin
      P := TProcedimento.Create;
      P.Id := Query.FieldByName('id').AsLargeInt;
      P.Tipo := TTipoProcedimento(Query.FieldByName('tipo').AsInteger);
      P.Descricao := Query.FieldByName('descricao').AsString;
      P.Valor := Query.FieldByName('valor').AsCurrency;
      Lista.Add(P);
      Query.Next;
    end;
    Result := Lista.ToArray;
  finally
    Query.Free;
    Lista.Free;
  end;
end;

function TProcedimentoSQLHandler.Inserir(const Tipo: TTipoProcedimento; const Descricao: string; const Valor: Currency): Boolean;
var
  Query: TFDQuery;
begin
  Result := False;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'INSERT INTO procedimentos (tipo, descricao, valor) VALUES (:tipo, :descricao, :valor)';
    Query.ParamByName('tipo').AsInteger := Ord(Tipo);
    Query.ParamByName('descricao').AsString := Descricao;
    Query.ParamByName('valor').AsCurrency := Valor;
    Query.ExecSQL;
    Result := True;
  finally
    Query.Free;
  end;
end;

function TProcedimentoSQLHandler.Atualizar(const Id: Int64; const Tipo: TTipoProcedimento; const Descricao: string; const Valor: Currency): Boolean;
var
  Query: TFDQuery;
begin
  Result := False;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'UPDATE procedimentos SET tipo = :tipo, descricao = :descricao, valor = :valor WHERE id = :id';
    Query.ParamByName('id').AsLargeInt := Id;
    Query.ParamByName('tipo').AsInteger := Ord(Tipo);
    Query.ParamByName('descricao').AsString := Descricao;
    Query.ParamByName('valor').AsCurrency := Valor;
    Query.ExecSQL;
    Result := True;
  finally
    Query.Free;
  end;
end;

function TProcedimentoSQLHandler.Excluir(const Id: Int64): Boolean;
var
  Query: TFDQuery;
begin
  Result := False;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT COUNT(*) FROM servicos_farmaceuticos_itens WHERE procedimento_id = :id';
    Query.ParamByName('id').AsLargeInt := Id;
    Query.Open;
    if Query.Fields[0].AsInteger > 0 then
      Exit(False); // Está em uso
    Query.Close;
    Query.SQL.Text := 'DELETE FROM procedimentos WHERE id = :id';
    Query.ParamByName('id').AsLargeInt := Id;
    Query.ExecSQL;
    Result := True;
  finally
    Query.Free;
  end;
end;

function TProcedimentoSQLHandler.Carregar(const Id: Int64; Procedimento: TProcedimento): Boolean;
var
  Query: TFDQuery;
begin
  Result := False;
  if not Assigned(Procedimento) then
    Exit;

  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT * FROM procedimentos WHERE id = :id';
    Query.ParamByName('id').AsLargeInt := Id;
    Query.Open;
    if not Query.IsEmpty then
    begin
      Procedimento.Id := Query.FieldByName('id').AsLargeInt;
      Procedimento.Tipo := TTipoProcedimento(Query.FieldByName('tipo').AsInteger);
      Procedimento.Descricao := Query.FieldByName('descricao').AsString;
      Procedimento.Valor := Query.FieldByName('valor').AsCurrency;
      Result := True;
    end
    else
      Procedimento.Limpar;
  finally
    Query.Free;
  end;
end;

end.

