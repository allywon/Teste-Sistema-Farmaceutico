unit Routes.ServicoFarmaceutico;

interface

uses
  System.SysUtils, System.Classes, IdHTTPServer, IdCustomHTTPServer,
  IdContext, System.JSON, IdGlobal, Routes.Interfaces,
  ServicoFarmaceutico.Controller, ModelDB, Model.ServicoFarmaceutico,
  Model.Pessoa, Model.Procedimento, Model.Procedimento.Tipo;

type
  TServicoFarmaceuticoRoute = class(TRouteBase)
  private
    FServicoFarmaceuticoController: TServicoFarmaceuticoController;

    procedure RotaServicos(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo);
    procedure RotaServicoById(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo; const Id: string);
    procedure RotaServicosPorPaciente(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo; const PacienteId: string);
    procedure RotaServicosPorFarmaceutico(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo; const FarmaceuticoId: string);
    procedure RotaServicosPorPeriodo(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo);
  public
    constructor Create(AServer: TIdHTTPServer; AModelDB: TModelDB);
    destructor Destroy; override;

    procedure RegistrarRotas; override;
    function ProcessarRota(const Path: string; const Verb: THTTPVerb;
      ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo): Boolean; override;
  end;

implementation

{ TServicoFarmaceuticoRoute }

constructor TServicoFarmaceuticoRoute.Create(AServer: TIdHTTPServer; AModelDB: TModelDB);
begin
  inherited Create(AServer);
  FServicoFarmaceuticoController := TServicoFarmaceuticoController.Create(AModelDB);
end;

destructor TServicoFarmaceuticoRoute.Destroy;
begin
  FServicoFarmaceuticoController.Free;
  inherited;
end;

procedure TServicoFarmaceuticoRoute.RegistrarRotas;
begin
  WriteLn('GET    /api/servicos-farmaceuticos');
  WriteLn('GET    /api/servicos-farmaceuticos/:id');
  WriteLn('GET    /api/servicos-farmaceuticos/paciente/:pacienteId');
  WriteLn('GET    /api/servicos-farmaceuticos/farmaceutico/:farmaceuticoId');
  WriteLn('GET    /api/servicos-farmaceuticos/periodo');
  WriteLn('POST   /api/servicos-farmaceuticos');
  WriteLn('PUT    /api/servicos-farmaceuticos/:id');
  WriteLn('DELETE /api/servicos-farmaceuticos/:id');
end;

function TServicoFarmaceuticoRoute.ProcessarRota(const Path: string; const Verb: THTTPVerb;
  ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo): Boolean;
var
  lIdParam: string;
  lPacienteIdParam: string;
  lFarmaceuticoIdParam: string;
begin
  Result := False;

  if Path = '/api/servicos-farmaceuticos' then
  begin
    if Verb in [hvGET, hvPOST] then
    begin
      RotaServicos(ARequestInfo, AResponseInfo);
      Result := True;
    end
    else
    begin
      AResponseInfo.ResponseNo := 405;
      Result := True;
    end;
  end
  else if Path = '/api/servicos-farmaceuticos/periodo' then
  begin
    if Verb = hvGET then
    begin
      RotaServicosPorPeriodo(ARequestInfo, AResponseInfo);
      Result := True;
    end
    else
    begin
      AResponseInfo.ResponseNo := 405;
      AResponseInfo.ContentText := '{"erro": "Método não permitido"}';
      Result := True;
    end;
  end
  else if Path.StartsWith('/api/servicos-farmaceuticos/paciente/') then
  begin
    lPacienteIdParam := ExtrairParamPath(Path, 4);
    if lPacienteIdParam <> '' then
    begin
      RotaServicosPorPaciente(ARequestInfo, AResponseInfo, lPacienteIdParam);
      Result := True;
    end
    else
    begin
      AResponseInfo.ResponseNo := 400;
      AResponseInfo.ContentText := '{"error": "ID do paciente não fornecido"}';
      Result := True;
    end;
  end
  else if Path.StartsWith('/api/servicos-farmaceuticos/farmaceutico/') then
  begin
    lFarmaceuticoIdParam := ExtrairParamPath(Path, 4);
    if lFarmaceuticoIdParam <> '' then
    begin
      RotaServicosPorFarmaceutico(ARequestInfo, AResponseInfo, lFarmaceuticoIdParam);
      Result := True;
    end
    else
    begin
      AResponseInfo.ResponseNo := 400;
      AResponseInfo.ContentText := '{"error": "ID do farmacêutico não fornecido"}';
      Result := True;
    end;
  end
  else if Path.StartsWith('/api/servicos-farmaceuticos/') then
  begin
    lIdParam := ExtrairParamPath(Path, 3);
    if lIdParam <> '' then
    begin
      RotaServicoById(ARequestInfo, AResponseInfo, lIdParam);
      Result := True;
    end
    else
    begin
      AResponseInfo.ResponseNo := 400;
      AResponseInfo.ContentText := '{"error": "ID não fornecido"}';
      Result := True;
    end;
  end;
end;

procedure TServicoFarmaceuticoRoute.RotaServicos(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo);
var
  lBodyJSON: TJSONObject;
  lResult: Boolean;
  lVerbo: THTTPVerb;
  lServico: TServicoFarmaceutico;
  lProcedimentoJSON: TJSONValue;
  lProcedimentosArray: TJSONArray;
  lProcedimento: TProcedimento;
  lPacienteJSON, lFarmaceuticoJSON: TJSONObject;
  i: Integer;
begin
  lVerbo := ObterVerboHTTP(ARequestInfo.Command);

  case lVerbo of
    hvGET:
      begin
        AResponseInfo.ResponseNo := 200;
        AResponseInfo.ContentText := FServicoFarmaceuticoController.ObterServicos.ToJSON;
      end;

    hvPOST:
      begin
        lBodyJSON := LerBodyComoJSON(ARequestInfo);
        if not Assigned(lBodyJSON) then
        begin
          AResponseInfo.ResponseNo := 400;
          AResponseInfo.ContentText := '{"error": "JSON inválido ou ausente"}';
          Exit;
        end;

        lServico := TServicoFarmaceutico.Create;
        try
          // Configurar data do serviço
          lServico.Data := lBodyJSON.GetValue<TDateTime>('data');
          lServico.Observacoes := lBodyJSON.GetValue<string>('observacoes');

          // Acessa o objeto "paciente" e depois o campo "id"
          lPacienteJSON := lBodyJSON.GetValue<TJSONObject>('paciente');
          lServico.Paciente.Id := lPacienteJSON.GetValue<Int64>('id');

          // Acessa o objeto "farmaceutico" e depois o campo "id"
          lFarmaceuticoJSON := lBodyJSON.GetValue<TJSONObject>('farmaceutico');
          lServico.Farmaceutico.Id := lFarmaceuticoJSON.GetValue<Int64>('id');

          // Configurar procedimentos
          if lBodyJSON.TryGetValue<TJSONArray>('procedimentos', lProcedimentosArray) then
          begin
            for i := 0 to lProcedimentosArray.Count - 1 do
            begin
              lProcedimentoJSON := lProcedimentosArray.Items[i];
              if lProcedimentoJSON is TJSONObject then
              begin
                lProcedimento := TProcedimento.Create;
                lProcedimento.Id := TJSONObject(lProcedimentoJSON).GetValue<Int64>('id');
                lProcedimento.Descricao := TJSONObject(lProcedimentoJSON).GetValue<string>('descricao');
                lProcedimento.Tipo := StrToTipoProcedimento(TJSONObject(lProcedimentoJSON).GetValue<string>('tipo'));
                lProcedimento.Valor := TJSONObject(lProcedimentoJSON).GetValue<Double>('valor');
                lServico.Procedimentos.Add(lProcedimento);
              end;
            end;
          end;

          lServico.CalcularValorTotal;

          lResult := FServicoFarmaceuticoController.InserirServico(lServico);
          if lResult then
          begin
            AResponseInfo.ResponseNo := 201;
            AResponseInfo.ContentText := '{"mensagem": "Serviço farmacêutico criado com sucesso"}';
          end
          else
          begin
            AResponseInfo.ResponseNo := 500;
            AResponseInfo.ContentText := '{"erro": "Falha ao inserir serviço farmacêutico"}';
          end;
        finally
          lBodyJSON.Free;
          lServico.Free;
        end;
      end;
  end;
end;

procedure TServicoFarmaceuticoRoute.RotaServicoById(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo; const Id: string);
var
  lId: Int64;
  lBodyJSON: TJSONObject;
  lResult: Boolean;
  lVerbo: THTTPVerb;
  lServico: TServicoFarmaceutico;
  lProcedimentoJSON: TJSONValue;
  lProcedimentosArray: TJSONArray;
  lProcedimento: TProcedimento;
  lPacienteJSON, lFarmaceuticoJSON: TJSONObject;
  i: Integer;
begin
  lVerbo := ObterVerboHTTP(ARequestInfo.Command);
  lId := StrToInt64Def(Id, 0);

  case lVerbo of
    hvGET:
      begin
        AResponseInfo.ResponseNo := 200;
        AResponseInfo.ContentText := FServicoFarmaceuticoController.ObterServicoPorId(lId).ToJSON;
      end;

    hvPUT:
      begin
        lBodyJSON := LerBodyComoJSON(ARequestInfo);
        if not Assigned(lBodyJSON) then
        begin
          AResponseInfo.ResponseNo := 400;
          AResponseInfo.ContentText := '{"error": "JSON inválido ou ausente"}';
          Exit;
        end;

        lServico := TServicoFarmaceutico.Create;
        try
          // Definir ID do serviço
          lServico.Id := lId;

          // Configurar data do serviço
          lServico.Data := lBodyJSON.GetValue<TDateTime>('data');
          lServico.Observacoes := lBodyJSON.GetValue<string>('observacoes');

          // Acessa o objeto "paciente" e depois o campo "id"
          lPacienteJSON := lBodyJSON.GetValue<TJSONObject>('paciente');
          lServico.Paciente.Id := lPacienteJSON.GetValue<Int64>('id');

          // Acessa o objeto "farmaceutico" e depois o campo "id"
          lFarmaceuticoJSON := lBodyJSON.GetValue<TJSONObject>('farmaceutico');
          lServico.Farmaceutico.Id := lFarmaceuticoJSON.GetValue<Int64>('id');

          // Configurar procedimentos
          if lBodyJSON.TryGetValue<TJSONArray>('procedimentos', lProcedimentosArray) then
          begin
            for i := 0 to lProcedimentosArray.Count - 1 do
            begin
              lProcedimentoJSON := lProcedimentosArray.Items[i];
              if lProcedimentoJSON is TJSONObject then
              begin
                lProcedimento := TProcedimento.Create;
                lProcedimento.Id := TJSONObject(lProcedimentoJSON).GetValue<Int64>('id');
                lProcedimento.Descricao := TJSONObject(lProcedimentoJSON).GetValue<string>('descricao');
                lProcedimento.Tipo := TTipoProcedimento(TJSONObject(lProcedimentoJSON).GetValue<Integer>('tipo'));
                lProcedimento.Valor := TJSONObject(lProcedimentoJSON).GetValue<Double>('valor');
                lServico.Procedimentos.Add(lProcedimento);
              end;
            end;
          end;

          lServico.CalcularValorTotal;

          lResult := FServicoFarmaceuticoController.AtualizarServico(lServico);
          if lResult then
          begin
            AResponseInfo.ResponseNo := 200;
            AResponseInfo.ContentText := '{"mensagem": "Serviço farmacêutico atualizado com sucesso"}';
          end
          else
          begin
            AResponseInfo.ResponseNo := 500;
            AResponseInfo.ContentText := '{"erro": "Falha ao atualizar serviço farmacêutico"}';
          end;
        finally
          lBodyJSON.Free;
          lServico.Free;
        end;
      end;

    hvDELETE:
      begin
        lResult := FServicoFarmaceuticoController.ExcluirServico(lId);
        if lResult then
        begin
          AResponseInfo.ResponseNo := 200;
          AResponseInfo.ContentText := '{"mensagem": "Serviço farmacêutico excluído com sucesso"}';
        end
        else
        begin
          AResponseInfo.ResponseNo := 404;
          AResponseInfo.ContentText := '{"erro": "Serviço farmacêutico não encontrado ou falha ao excluir"}';
        end;
      end;
  end;
end;

procedure TServicoFarmaceuticoRoute.RotaServicosPorPaciente(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo; const PacienteId: string);
var
  lPacienteId: Int64;
  lVerbo: THTTPVerb;
begin
  lVerbo := ObterVerboHTTP(ARequestInfo.Command);
  lPacienteId := StrToInt64Def(PacienteId, 0);

  if lVerbo = hvGET then
  begin
    AResponseInfo.ResponseNo := 200;
    AResponseInfo.ContentText := FServicoFarmaceuticoController.ObterServicosPorPaciente(lPacienteId).ToJSON;
  end
  else
  begin
    AResponseInfo.ResponseNo := 405;
    AResponseInfo.ContentText := '{"erro": "Método não permitido"}';
  end;
end;

procedure TServicoFarmaceuticoRoute.RotaServicosPorFarmaceutico(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo; const FarmaceuticoId: string);
var
  lFarmaceuticoId: Int64;
  lVerbo: THTTPVerb;
begin
  lVerbo := ObterVerboHTTP(ARequestInfo.Command);
  lFarmaceuticoId := StrToInt64Def(FarmaceuticoId, 0);

  if lVerbo = hvGET then
  begin
    AResponseInfo.ResponseNo := 200;
    AResponseInfo.ContentText := FServicoFarmaceuticoController.ObterServicosPorFarmaceutico(lFarmaceuticoId).ToJSON;
  end
  else
  begin
    AResponseInfo.ResponseNo := 405;
    AResponseInfo.ContentText := '{"erro": "Método não permitido"}';
  end;
end;

procedure TServicoFarmaceuticoRoute.RotaServicosPorPeriodo(ARequestInfo: TIdHTTPRequestInfo; AResponseInfo: TIdHTTPResponseInfo);
var
  lVerbo: THTTPVerb;
  lDataInicio, lDataFim: TDateTime;
begin
  lVerbo := ObterVerboHTTP(ARequestInfo.Command);

  if lVerbo = hvGET then
  begin
    if (ARequestInfo.Params.Values['dataInicio'] <> '') and (ARequestInfo.Params.Values['dataFim'] <> '') then
    begin
      try
        lDataInicio := StrToDateTime(ARequestInfo.Params.Values['dataInicio']);
        lDataFim := StrToDateTime(ARequestInfo.Params.Values['dataFim']);

        AResponseInfo.ResponseNo := 200;
        AResponseInfo.ContentText := FServicoFarmaceuticoController.ObterServicosPorPeriodo(lDataInicio, lDataFim).ToJSON;
      except
        on E: Exception do
        begin
          AResponseInfo.ResponseNo := 400;
          AResponseInfo.ContentText := '{"erro": "Formato de data inválido. Use o formato DD/MM/YYYY"}';
        end;
      end;
    end
    else
    begin
      AResponseInfo.ResponseNo := 400;
      AResponseInfo.ContentText := '{"erro": "Os parâmetros dataInicio e dataFim são obrigatórios"}';
    end;
  end
  else
  begin
    AResponseInfo.ResponseNo := 405;
    AResponseInfo.ContentText := '{"erro": "Método não permitido"}';
  end;
end;

end.
