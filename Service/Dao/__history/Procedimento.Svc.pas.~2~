unit Procedimento.Svc;

interface

uses
  FireDAC.Comp.Client, ModelDB, Model.Procedimento, Model.Procedimento.Tipo,
  System.Generics.Collections, System.SysUtils;

type
  TProcedimentoSQLHandler = class
  private
    FDB: TModelDB;
  public
    constructor Create(ADB: TModelDB);

    function ListarTodos: TArray<TProcedimento>;
    function BuscarPorId(const AId: Int64): TProcedimento;
    function BuscarPorTipo(const ATipo: TTipoProcedimento): TArray<TProcedimento>;

    function Inserir(AProcedimento: TProcedimento): Boolean;
    function Atualizar(const AId: Int64; AProcedimento: TProcedimento): Boolean;
    function Excluir(const AId: Int64): Boolean;

    function Carregar(const AId: Int64; AProcedimento: TProcedimento): Boolean;
  end;

implementation

{ TProcedimentoSQLHandler }

constructor TProcedimentoSQLHandler.Create(ADB: TModelDB);
begin
  FDB := ADB;
end;

function TProcedimentoSQLHandler.ListarTodos: TArray<TProcedimento>;
var
  Query: TFDQuery;
  Lista: TList<TProcedimento>;
  P: TProcedimento;
begin
  Lista := TList<TProcedimento>.Create;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT * FROM procedimentos ORDER BY tipo';
    Query.Open;
    while not Query.Eof do
    begin
      P := TProcedimento.Create;
      P.Id := Query.FieldByName('id').AsLargeInt;
      P.Tipo := TTipoProcedimento(Query.FieldByName('tipo').AsInteger);
      P.Descricao := Query.FieldByName('descricao').AsString;
      P.Valor := Query.FieldByName('valor').AsCurrency;
      Lista.Add(P);
      Query.Next;
    end;
    Result := Lista.ToArray;
  finally
    Query.Free;
    Lista.Free;
  end;
end;

function TProcedimentoSQLHandler.BuscarPorId(const AId: Int64): TProcedimento;
var
  Query: TFDQuery;
begin
  Result := nil;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT * FROM procedimentos WHERE id = :id';
    Query.ParamByName('id').AsLargeInt := AId;
    Query.Open;
    if not Query.IsEmpty then
    begin
      Result := TProcedimento.Create;
      Result.Id := Query.FieldByName('id').AsLargeInt;
      Result.Tipo := TTipoProcedimento(Query.FieldByName('tipo').AsInteger);
      Result.Descricao := Query.FieldByName('descricao').AsString;
      Result.Valor := Query.FieldByName('valor').AsCurrency;
    end;
  finally
    Query.Free;
  end;
end;

function TProcedimentoSQLHandler.BuscarPorTipo(const ATipo: TTipoProcedimento): TArray<TProcedimento>;
var
  Query: TFDQuery;
  Lista: TList<TProcedimento>;
  P: TProcedimento;
begin
  Lista := TList<TProcedimento>.Create;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT * FROM procedimentos WHERE tipo = :tipo ORDER BY descricao';
    Query.ParamByName('tipo').AsInteger := Ord(ATipo);
    Query.Open;
    while not Query.Eof do
    begin
      P := TProcedimento.Create;
      P.Id := Query.FieldByName('id').AsLargeInt;
      P.Tipo := TTipoProcedimento(Query.FieldByName('tipo').AsInteger);
      P.Descricao := Query.FieldByName('descricao').AsString;
      P.Valor := Query.FieldByName('valor').AsCurrency;
      Lista.Add(P);
      Query.Next;
    end;
    Result := Lista.ToArray;
  finally
    Query.Free;
    Lista.Free;
  end;
end;

function TProcedimentoSQLHandler.Inserir(AProcedimento: TProcedimento): Boolean;
var
  Query: TFDQuery;
begin
  Result := False;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'INSERT INTO procedimentos (tipo, descricao, valor) VALUES (:tipo, :descricao, :valor)';
    Query.ParamByName('tipo').AsInteger := Ord(AProcedimento.Tipo);
    Query.ParamByName('descricao').AsString := AProcedimento.Descricao;
    Query.ParamByName('valor').AsCurrency := AProcedimento.Valor;
    Query.ExecSQL;
    Result := True;
  finally
    Query.Free;
  end;
end;

function TProcedimentoSQLHandler.Atualizar(const AId: Int64; AProcedimento: TProcedimento): Boolean;
var
  Query: TFDQuery;
begin
  Result := False;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'UPDATE procedimentos SET tipo = :tipo, descricao = :descricao, valor = :valor WHERE id = :id';
    Query.ParamByName('id').AsLargeInt := AId;
    Query.ParamByName('tipo').AsInteger := Ord(AProcedimento.Tipo);
    Query.ParamByName('descricao').AsString := AProcedimento.Descricao;
    Query.ParamByName('valor').AsCurrency := AProcedimento.Valor;
    Query.ExecSQL;
    Result := True;
  finally
    Query.Free;
  end;
end;

function TProcedimentoSQLHandler.Excluir(const AId: Int64): Boolean;
var
  Query: TFDQuery;
begin
  Result := False;
  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT COUNT(*) FROM servicos_farmaceuticos_itens WHERE procedimento_id = :id';
    Query.ParamByName('id').AsLargeInt := AId;
    Query.Open;
    if Query.Fields[0].AsInteger > 0 then
      Exit(False); // Está em uso
    Query.Close;
    Query.SQL.Text := 'DELETE FROM procedimentos WHERE id = :id';
    Query.ParamByName('id').AsLargeInt := AId;
    Query.ExecSQL;
    Result := True;
  finally
    Query.Free;
  end;
end;

function TProcedimentoSQLHandler.Carregar(const AId: Int64; AProcedimento: TProcedimento): Boolean;
var
  Query: TFDQuery;
begin
  Result := False;
  if not Assigned(Procedimento) then
    Exit;

  Query := TFDQuery.Create(nil);
  try
    Query.Connection := FDB.FDConnection;
    Query.SQL.Text := 'SELECT * FROM procedimentos WHERE id = :id';
    Query.ParamByName('id').AsLargeInt := AId;
    Query.Open;
    if not Query.IsEmpty then
    begin
      AProcedimento.Id := Query.FieldByName('id').AsLargeInt;
      AProcedimento.Tipo := TTipoProcedimento(Query.FieldByName('tipo').AsInteger);
      AProcedimento.Descricao := Query.FieldByName('descricao').AsString;
      AProcedimento.Valor := Query.FieldByName('valor').AsCurrency;
      Result := True;
    end
    else
      AProcedimento.Limpar;
  finally
    Query.Free;
  end;
end;

end.

